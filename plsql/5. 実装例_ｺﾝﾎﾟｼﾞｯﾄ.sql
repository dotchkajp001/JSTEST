CREATE OR REPLACE TYPE ABSORDER AS OBJECT
(
	ID NUMBER
	-- 注文を追加します
	,MEMBER PROCEDURE ADDORDER(VAL1 ABSORDER)
	-- 料金を集計する抽象ﾒｿｯﾄﾞです
	,NOT INSTANTIABLE MEMBER FUNCTION GET_AMOUNT RETURN NUMBER
) NOT FINAL NOT INSTANTIABLE;
/
--
CREATE OR REPLACE TYPE ORDERITEM UNDER ABSORDER
(
	-- 価格
	PRICE NUMBER
	-- 価格計算
	,OVERRIDING MEMBER FUNCTION GET_AMOUNT RETURN NUMBER
);
/
--
CREATE OR REPLACE TYPE BODY ORDERITEM
AS
	-- 価格計算
	OVERRIDING MEMBER FUNCTION GET_AMOUNT RETURN NUMBER
	IS
	BEGIN
		RETURN PRICE;
	END GET_AMOUNT;
END;
/
--
CREATE TYPE ABSORDER_LIST IS VARRAY(100) OF ABSORDER;
/
--
CREATE OR REPLACE TYPE ORDERLINE UNDER ABSORDER
(
	-- 注文ﾘｽﾄ
	LIST ABSORDER_LIST
	-- 注文ﾘｽﾄに注文を追加する
	,OVERRIDING MEMBER PROCEDURE ADDORDER(VAL1 ABSORDER)
	-- 価格計算(注文の合計)
	,OVERRIDING MEMBER FUNCTION GET_AMOUNT RETURN NUMBER
);
/
--
CREATE OR REPLACE TYPE BODY ORDERLINE
AS
	-- 注文ﾘｽﾄに注文を追加する
	OVERRIDING MEMBER PROCEDURE ADDORDER(VAL1 ABSORDER)
	IS
	BEGIN
		LIST.EXTEND;
		LIST(LIST.LAST)	:= VAL1;
	END ADDORDER;
	-- 価格計算(注文の合計)
	OVERRIDING MEMBER FUNCTION GET_AMOUNT RETURN NUMBER
	IS
		TOTAL NUMBER := 0;
		ITEM ABSORDER;
	BEGIN
		FOR I IN 1..LIST.COUNT LOOP
			ITEM := LIST(I);
			TOTAL := TOTAL + ITEM.GET_AMOUNT;
		END LOOP;
		RETURN TOTAL;
	END GET_AMOUNT;
END;
/
--
SET SERVEROUTPUT ON
DECLARE
	OBJ_LIST ABSORDER_LIST;
	LINE_A ORDERLINE;
	ITEM_1 ORDERITEM;
	LINE_B ORDERLINE;
	ITEM_2 ORDERITEM;
	ITEM_3 ORDERITEM;
BEGIN
	-- 注文ﾘｽﾄAの生成
	LINE_A := ORDERLINE(1, ABSORDER_LIST());
	
	-- 注文ﾘｽﾄAにｱｲﾃﾑを追加
	ITEM_1 := ORDERITEM(2, 100);
	LINE_A.ADDORDER(ITEM_1);
	
	-- 注文ﾘｽﾄBの生成
	LINE_B := ORDERLINE(3, ABSORDER_LIST());
	
	-- 注文ﾘｽﾄBにｱｲﾃﾑを追加
	ITEM_2 := ORDERITEM(4,50);
	LINE_B.ADDORDER(ITEM_2);
	
	-- 注文ﾘｽﾄBにｱｲﾃﾑを追加
	ITEM_3 := ORDERITEM(5,70);
	LINE_B.ADDORDER(ITEM_3);
	
	-- 注文ﾘｽﾄAに注文ﾘｽﾄBを追加
	LINE_A.ADDORDER(LINE_B);

	-- 注文ﾘｽﾄAと注文ﾘｽﾄBの合計が出力されます
	DBMS_OUTPUT.PUT_LINE('注文ﾘｽﾄAの合計金額は：' || TO_CHAR(LINE_A.GET_AMOUNT) || 'です');
	-- 注文ﾘｽﾄBの合計が出力されます
	DBMS_OUTPUT.PUT_LINE('注文ﾘｽﾄBの合計金額は：' || TO_CHAR(LINE_B.GET_AMOUNT) || 'です');
END;
/

/** 削除処理
DROP TYPE ORDERLINE;
DROP TYPE ABSORDER_LIST;
DROP TYPE ORDERITEM;
DROP TYPE ABSORDER;
**/