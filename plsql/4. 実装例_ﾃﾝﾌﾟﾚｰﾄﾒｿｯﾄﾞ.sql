-- ﾃﾝﾌﾟﾚｰﾄﾒｿｯﾄﾞの実装例
-- 親ｵﾌﾞｼﾞｪｸﾄ(抽象ｵﾌﾞｼﾞｪｸﾄ)の実装
CREATE OR REPLACE TYPE TEMPLATE_OYA AS OBJECT(
	VAL1	NUMBER
	,VAL2	NUMBER
	,NOT INSTANTIABLE MEMBER FUNCTION	CHECK1 RETURN NUMBER
	,NOT INSTANTIABLE MEMBER PROCEDURE	CALC
	,MEMBER PROCEDURE DO
) NOT FINAL NOT INSTANTIABLE;
/
--
CREATE OR REPLACE TYPE BODY TEMPLATE_OYA
AS
	MEMBER PROCEDURE DO
	IS
	BEGIN
		-- ﾁｪｯｸ処理の実行
		IF CHECK1 = 0 THEN
			CALC;
		ELSE
			DBMS_OUTPUT.PUT_LINE('ｴﾗｰです');
		END IF;
	END DO;
END;
/
-- 子ｵﾌﾞｼﾞｪｸﾄ(加算)の実装
CREATE OR REPLACE TYPE TEMPLATE_PLUS UNDER TEMPLATE_OYA
(
	OVERRIDING MEMBER FUNCTION	CHECK1 RETURN NUMBER
	,OVERRIDING MEMBER PROCEDURE CALC
)
/
--
CREATE OR REPLACE TYPE BODY TEMPLATE_PLUS
AS
	OVERRIDING MEMBER FUNCTION	CHECK1 RETURN NUMBER
	IS
	BEGIN
		-- ｴﾗｰﾁｪｯｸ無し
		RETURN 0;
	END CHECK1;
	OVERRIDING MEMBER PROCEDURE	CALC
	IS
	BEGIN
		DBMS_OUTPUT.PUT_LINE('計算結果：' || TO_CHAR(VAL1 + VAL2));
	END CALC;
END;
/
-- 子ｵﾌﾞｼﾞｪｸﾄ(割り算)の実装
CREATE OR REPLACE TYPE TEMPLATE_DIV UNDER TEMPLATE_OYA
(
	OVERRIDING MEMBER FUNCTION	CHECK1 RETURN NUMBER
	,OVERRIDING MEMBER PROCEDURE CALC
)
/
--
CREATE OR REPLACE TYPE BODY TEMPLATE_DIV
AS
	OVERRIDING MEMBER FUNCTION	CHECK1 RETURN NUMBER
	IS
	BEGIN
		-- VAL2が0以下の場合はｴﾗｰ
		IF VAL2 <= 0 THEN
			RETURN 1;
		END IF;
		
		RETURN 0;
	END CHECK1;
	OVERRIDING MEMBER PROCEDURE	CALC
	IS
	BEGIN
		DBMS_OUTPUT.PUT_LINE('計算結果：' || TO_CHAR(VAL1 / VAL2));
	END CALC;
END;
/
--
/** ﾃｽﾄ処理の実行 **/
SET SERVEROUTPUT ON
DECLARE
	VAL1 NUMBER := 100;
	VAL2 NUMBER := 0;
	OBJ TEMPLATE_OYA;
BEGIN
	FOR I IN 0..1 LOOP
		IF I = 0 THEN
			-- ﾌﾟﾗｽ計算の場合
			OBJ	:= TEMPLATE_PLUS(VAL1,VAL2);
			DBMS_OUTPUT.PUT_LINE('加算');
		ELSE
			-- 割り算の場合
			OBJ	:= TEMPLATE_DIV(VAL1,VAL2);
			DBMS_OUTPUT.PUT_LINE('割り算');
		END IF;
--
		-- 計算処理の実行
		OBJ.DO;
	END LOOP;
END;
/

/** 削除
DROP TYPE TEMPLATE_DIV;
DROP TYPE TEMPLATE_PLUS;
DROP TYPE TEMPLATE_OYA;
**/