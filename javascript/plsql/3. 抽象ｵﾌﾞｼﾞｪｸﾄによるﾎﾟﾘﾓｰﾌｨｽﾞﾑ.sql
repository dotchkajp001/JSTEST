-- ﾎﾟﾘﾓｰﾌｨｽﾞﾑ実装ｻﾝﾌﾟﾙ
CREATE OR REPLACE TYPE OBJ_SAMPLE03_OYA AS OBJECT
(
	VAL1 NUMBER
	,VAL2 VARCHAR2(100)
	,NOT INSTANTIABLE MEMBER PROCEDURE PRO01
	,FINAL MEMBER FUNCTION FNC01(VAL1 VARCHAR2) RETURN NUMBER
)NOT FINAL NOT INSTANTIABLE;
/
--
CREATE OR REPLACE TYPE BODY OBJ_SAMPLE03_OYA
AS
	FINAL MEMBER FUNCTION FNC01(VAL1 VARCHAR2) RETURN NUMBER
	IS
	BEGIN
		DBMS_OUTPUT.PUT_LINE(VAL1);
		RETURN 0;
	END FNC01;
END;
/
--
CREATE OR REPLACE TYPE OBJ_SAMPLE03_KOA UNDER OBJ_SAMPLE03_OYA
(
	OVERRIDING MEMBER PROCEDURE PRO01
);
/
CREATE OR REPLACE TYPE BODY OBJ_SAMPLE03_KOA
AS
	OVERRIDING MEMBER PROCEDURE PRO01
	IS
	BEGIN
		DBMS_OUTPUT.PUT_LINE('子Aの処理です。');
	END PRO01;
END;
/
CREATE OR REPLACE TYPE OBJ_SAMPLE03_KOB UNDER OBJ_SAMPLE03_OYA
(
	OVERRIDING MEMBER PROCEDURE PRO01
);
/
--
CREATE OR REPLACE TYPE BODY OBJ_SAMPLE03_KOB
AS
	OVERRIDING MEMBER PROCEDURE PRO01
	IS
	BEGIN
		DBMS_OUTPUT.PUT_LINE('子Bの処理です。');
	END PRO01;
END;
/
-- ｻﾝﾌﾟﾙ処理
DECLARE
	OBJ OBJ_SAMPLE03_OYA;
BEGIN
	DBMS_OUTPUT.PUT_LINE('SAMPLE');
	FOR I IN 0..1 LOOP
		IF I = 0 THEN
			OBJ := OBJ_SAMPLE03_KOA(1,'子A');
		ELSE
			OBJ := OBJ_SAMPLE03_KOB(2,'子B');
		END IF;
		-- 同じﾌﾟﾛｼｰｼﾞｬの呼出で、で異なる処理が実行される(ﾎﾟﾘﾓｰﾌｨｽﾞﾑ)
		OBJ.PRO01;
	END LOOP;
END;
/
/** ｻﾝﾌﾟﾙの削除
	DROP TYPE OBJ_SAMPLE03_KOA;
	DROP TYPE OBJ_SAMPLE03_KOB;
	DROP TYPE OBJ_SAMPLE03_OYA;
**/